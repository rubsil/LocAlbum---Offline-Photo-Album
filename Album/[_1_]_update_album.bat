@echo off
setlocal EnableExtensions EnableDelayedExpansion
chcp 65001 >nul
title Update LOCALBUM

echo.
echo =====================================================
echo          LOCALBUM - OFFLINE PHOTO ALBUM
echo =====================================================
echo.

:: === detetar local atual ===
set "ROOT=%~dp0"
if "%ROOT:~-1%"=="\" set "ROOT=%ROOT:~0,-1%"

:: === caminhos importantes ===
set "PS1=%ROOT%\[1].ps1"
set "INI=%ROOT%\config.ini"

:: === garantir favicon ===
if not exist "%ROOT%\favicon.png" (
  if exist "%ROOT%\assets\favicon_base.png" (
    copy "%ROOT%\assets\favicon_base.png" "%ROOT%\favicon.png" >nul
    echo Favicon padrÃ£o criado.
  ) else (
    echo âš ï¸ Nenhum favicon encontrado. O HTML serÃ¡ criado sem Ã­cone.
  )
)

:: === verificar existencia do PowerShell script ===
if not exist "%PS1%" (
  echo.
  echo ERRO: O ficheiro PowerShell nao foi encontrado.
  echo Esperado em: "%PS1%"
  echo.
  pause
  exit /b 1
)

echo Pasta detetada: %ROOT%
echo.

:: ===== primeira execucao (cria config.ini oculto) =====
if not exist "%INI%" goto :first_run
goto :after_questions


:first_run
echo =====================================================
echo LOCALBUM Offline Photo Album - primeira configuracao
echo =====================================================
echo.

echo ðŸŒ Escolhe o idioma / Choose language:
echo [P] PT  â†’ PortuguÃªs
echo [E] EN  â†’ English
echo.

choice /c PE /m "Seleciona uma opÃ§Ã£o:"

if %errorlevel%==1 goto :ask_pt
if %errorlevel%==2 goto :ask_en


:ask_pt
set "LANG=pt"
cls
echo.
echo =====================================================
echo   Bem-vindo Ã  configuraÃ§Ã£o inicial do teu Ã¡lbum :)
echo =====================================================
echo.
echo Vamos fazer-te algumas perguntas. Podes mudar tudo depois.
echo.
echo -----------------------------------------------------
set /p ALBUM_NAME="TÃ­tulo do Ã¡lbum (ex.: MemÃ³rias do Martim): "
if "!ALBUM_NAME!"=="" set "ALBUM_NAME=MemÃ³rias"
echo -----------------------------------------------------
echo.
echo (OPCIONAL) Insere data de nascimento para mostrar a idade
echo            Ã  data de cada foto visualizada.
echo            (Ãštil caso seja um bebÃ©.)
echo   (NÃ£o insiras nada caso nÃ£o queiras esta funcionalidade)
echo.
set /p BIRTHDATE="Data de nascimento (formato: AAAA-MM-DD): "
goto :write_ini


:ask_en
set "LANG=en"
cls
echo.
echo =====================================================
echo   Welcome to the initial album setup :)
echo =====================================================
echo.
echo Weâ€™ll ask you a few quick questions. You can change them later.
echo.
echo -----------------------------------------------------
set /p ALBUM_NAME="Album title (e.g., Memories of Emma): "
if "!ALBUM_NAME!"=="" set "ALBUM_NAME=Memories"
echo -----------------------------------------------------
echo.
echo (OPTIONAL) Enter date of birth to display the age
echo            as of the date of each viewed photo.
echo            (Useful if itâ€™s a baby.)
echo   (Leave blank if you donâ€™t want this feature)
echo.
set /p BIRTHDATE="Date of birth (format: YYYY-MM-DD): "
goto :write_ini


:write_ini
for /f "tokens=* delims=" %%A in ("!ALBUM_NAME!") do set "ALBUM_NAME=%%A"
for /f "tokens=* delims=" %%A in ("!BIRTHDATE!") do set "BIRTHDATE=%%A"

set "PAGE_TITLE=LOCALBUM - Offline Photo Album"
set "THEME=dark"
set "DONATE_URL=https://www.paypal.me/rubsil"
set "AUTHOR=Ruben Silva"
set "PROJECT=LOCALBUM - Offline Photo Album"

(
echo [album]
echo language=%LANG%
echo display_name=%ALBUM_NAME%
echo page_title=%PAGE_TITLE%
echo birthdate=%BIRTHDATE%
echo theme=%THEME%
echo donate_url=%DONATE_URL%
echo author=%AUTHOR%
echo project_name=%PROJECT%
)>"%INI%"

attrib +h "%INI%"
echo.
goto :after_questions


:after_questions
if not defined LANG set "LANG=pt"

echo.
if /I "!LANG!"=="en" (
  echo Reading photos from: "%ROOT%\Fotos"
  echo Generating HTML at:  "%ROOT%\album.html"
) else (
  echo Lendo fotos em: "%ROOT%\Fotos"
  echo Gerando HTML em:  "%ROOT%\album.html"
)
echo.

:: === chamar PowerShell ===
set "PWSH=%SystemRoot%\System32\WindowsPowerShell\v1.0\powershell.exe"
if not exist "%PWSH%" set "PWSH=powershell.exe"

"%PWSH%" -NoLogo -NoProfile -ExecutionPolicy Bypass -File "%PS1%"
set "ERR=%ERRORLEVEL%"

echo.
if not "%ERR%"=="0" (
  echo ERRO: O PowerShell devolveu o codigo %ERR%.
) else (
  echo.
  echo LOCAlbum atualizado com sucesso.
)
echo.
pause

